#
# CyberCache Cluster Test Suite
# Written by Vadim Sytnikov
# Copyright (C) 2016-2019 CyberHULL. All rights reserved.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
# -----------------------------------------------------------------------------
#
# Commands that test FPC store.
#
print "------------------"
print "  FPC store test  "
print "------------------"

# in case this file is run on its own
admin 'Test-Admin-Password-1'

print "----- Binlog saving/restoration commands:"
#
# Records loaded from binlog files are:
#
# From both "data/fpc-1.binlog" and "data/fpc-2.binlog":
#
#   'binlog-record' TAGS:[ 'binlog-tag-one' 'binlog-tag-two' ] DATA:[ 'Sample FPC record' ]
#
# From "data/fpc-2.binlog" only:
#
#   'another-record' TAGS:[ 'binlog-tag-three' ] DATA:[ 'Another FPC record' ]
#
restore data/fpc-2.binlog
checkresult ok
wait 100
load binlog-record
checkresult data 0 'Sample FPC record'
load another-record
checkresult data 0 'Another FPC record'
getids
checkresult list binlog-record another-record
gettags
checkresult list binlog-tag-one binlog-tag-two binlog-tag-three
# load "smaller" file, make sure that nothing is lost
restore data/fpc-1.binlog
checkresult ok
load another-record
checkresult data 0 'Another FPC record'
store fpc logs/fpc-store.blf
checkresult ok

print "----- FPC commands:"
#
# Record created by this script is:
#
#   'test' TAGS:[ 'first' 'binlog-tag-two' ] DATA:[ 'FPC record creation test' ]
#
# Later it is changed to:
#
#   'test' TAGS:[ 'first' 'binlog-tag-two' ] DATA:[ contents of the "data/sample-record.txt" file ]
#
tags first binlog-tag-two
save test 'FPC record creation test'
checkresult ok
load test
checkresult data 4 "record"
getidsmatchingtags binlog-tag-two binlog-tag-three
checkresult list another-record
getidsnotmatchingtags binlog-tag-three
checkresult list binlog-record test
getidsmatchinganytags first binlog-tag-two binlog-tag-three
checkresult list binlog-record another-record test
test test
checkresult string ':'
touch test 7d
checkresult ok
remove test
checkresult ok
test test
checkresult ok # meaning "not found"
touch test 1w
checkresult error 'did not exist'
load test
checkresult ok # meaning "not found"
compressor zstd
save test @data/sample-record.txt
checkresult ok
load test
checkresult data 0xA31 Fhtagn
checkresult data 0 @data/sample-record.txt
getfillingpercentage
checkresult string
getmetadatas test
checkresult string 'List' 'first' 'binlog-tag-two'

print "----- Cleaning FPC store:"

clean old
checkresult ok
load binlog-record
checkresult data 7 'FPC'
clean matchall first second third # should not clean any since tags 'second' and 'third' do not exist
checkresult ok
load binlog-record
checkresult data
clean matchany binlog-tag-one
checkresult ok
load binlog-record
checkresult ok # meaning "not found"
getids
checkresult list another-record test
clean matchnot first
checkresult ok
load another-record
checkresult ok # meaning "not found"
getids
checkresult list test
clean all
checkresult ok
load test
checkresult ok # meaning "not found"

print "--------------------------"
print "  FPC store test PASSED.  "
print "--------------------------"
